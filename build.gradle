allprojects {
    apply plugin: 'java'
    apply plugin: 'idea'

    version = '1.0.0-SNAPSHOT'
    group = 'com.zenika.dorm'

    sourceCompatibility = '1.6'
    targetCompatibility = '1.6'

    ideaModule {
        beforeConfigured { module ->
            module.dependencies.clear()
        }
    }

    configurations {
        sharedTests
        // commons-logging artefacts are useless (using SLF4J and Logback)
        all*.exclude group: 'commons-logging'
    }

    task testJar(type: Jar, dependsOn: testClasses) {
        baseName = "test-${project.archivesBaseName}"
        from sourceSets.test.classes
    }

    artifacts {
        sharedTests testJar
    }
}

ideaProject {
    // Setting Java version to use in IDEA
    javaVersion = "1.6"
    // Setting GIT as VCS
    withXml { provider ->
        def node = provider.asNode()
        def vcsConfig = node.component.find { it.'@name' == 'VcsDirectoryMappings' }
        vcsConfig.mapping[0].'@vcs' = 'Git'
    }
}

// Trying to get a kind of Maven's dependencyManagement
// 1st step, introducing general properties
versions = [
        slf4j           : '1.6.1',
        logback         : '0.9.29',
        jersey          : '1.8',
        jackson         : '1.8.4'
]

// 2nd step, introducing libraries
libraries = [
        slf4j_api           : 'org.slf4j:slf4j-api:' + versions.slf4j,
        slf4j_jcl           : 'org.slf4j:jcl-over-slf4j:' + versions.slf4j,
        slf4j_jul           : 'org.slf4j:jul-to-slf4j:' + versions.slf4j,
        slf4j_log4j         : 'org.slf4j:log4j-over-slf4j:' + versions.slf4j,
        logback_core        : 'ch.qos.logback:logback-core:' + versions.logback,
        logback_classic     : 'ch.qos.logback:logback-classic:' + versions.logback,

        jersey_server       : 'com.sun.jersey:jersey-server:' + versions.jersey,
        jersey_json         : 'com.sun.jersey:jersey-json:' + versions.jersey,
        jersey_client       : 'com.sun.jersey:jersey-client:' + versions.jersey,
        jersey_multipart    : 'com.sun.jersey.contribs:jersey-multipart:' + versions.jersey,
        jackson_mrbean      : 'org.codehaus.jackson:jackson-mrbean:' + versions.jackson
]

subprojects {

    configurations.compile.transitive = true

    guiceVersion = '3.0'
    junitVersion = '4.8.2'
    morphiaVersion = '0.99'

    repositories {
        mavenCentral()
        mavenRepo name: 'java.net repository', urls: "http://download.java.net/maven/2"
        mavenRepo name: 'morphia repository', urls: "http://morphia.googlecode.com/svn/mavenrepo"
    }

    dependencies {
        compile libraries.slf4j_api
        compile libraries.slf4j_jcl
        compile libraries.slf4j_jul
        compile libraries.slf4j_log4j
        compile libraries.logback_core
        compile libraries.logback_classic

        compile libraries.jersey_server
        compile libraries.jersey_json
        compile libraries.jersey_client
        compile libraries.jersey_multipart
        compile libraries.jackson_mrbean

        compile group: 'commons-io', name: 'commons-io', version: '2.0.1'
        compile group: 'com.google.inject', name: 'guice', version: guiceVersion
        compile group: 'com.google.code.morphia', name: 'morphia', version: morphiaVersion

        testCompile group: 'junit', name: 'junit', version: '4.8.2'
        testCompile group: 'org.mockito', name: 'mockito-all', version: '1.8.5'
        testCompile group: 'org.easytesting', name: 'fest-assert', version: '1.4'
    }
}

project(':dorm') {
    apply plugin: 'war'

    dependencies {
        compile project(':dorm-core'), project(':dorm-maven')
    }
}

project(':dorm-maven') {
    dependencies {
        compile project(':dorm-core')
        testCompile project(path: ':dorm-core', configuration: 'sharedTests')
    }
}

project(':dorm-core-test') {
    dependencies {
        compile project(':dorm-core')
    }
}