allprojects {
    apply plugin: 'java'
    apply plugin: 'idea'

    version = '1.0.0-SNAPSHOT'
    group = 'com.zenika.dorm'

    sourceCompatibility = '1.6'
    targetCompatibility = '1.6'

    ideaModule {
        beforeConfigured { module ->
            module.dependencies.clear()
        }
    }

    configurations {
        sharedTests
    }

    task testJar(type: Jar, dependsOn: testClasses) {
        baseName = "test-${project.archivesBaseName}"
        from sourceSets.test.classes
    }

    artifacts {
        sharedTests testJar
    }
}

ideaProject {
    // Setting Java version to use in IDEA
    javaVersion = "1.6"
    // Setting GIT as VCS
    withXml { provider ->
        def node = provider.asNode()
        def vcsConfig = node.component.find { it.'@name' == 'VcsDirectoryMappings' }
        vcsConfig.mapping[0].'@vcs' = 'Git'
    }
}

subprojects {

    configurations.compile.transitive = true

    jerseyVersion = '1.8'
    guiceVersion = '3.0'
    junitVersion = '4.8.2'
    morphiaVersion = '0.99'

    repositories {
        mavenCentral()
        mavenRepo name: 'java.net repository', urls: "http://download.java.net/maven/2"
        mavenRepo name: 'morphia repository', urls: "http://morphia.googlecode.com/svn/mavenrepo"
    }

    dependencies {
        compile group: 'commons-io', name: 'commons-io', version: '2.0.1'
        compile group: 'com.sun.jersey', name: 'jersey-server', version: jerseyVersion
        compile group: 'com.sun.jersey', name: 'jersey-json', version: jerseyVersion
        compile group: 'com.sun.jersey', name: 'jersey-client', version: jerseyVersion
        compile group: 'com.sun.jersey.contribs', name: 'jersey-multipart', version: jerseyVersion
        compile group: 'com.google.inject', name: 'guice', version: guiceVersion
        compile group: 'com.google.code.morphia', name: 'morphia', version: morphiaVersion

        testCompile group: 'junit', name: 'junit', version: '4.8.2'
        testCompile group: 'org.mockito', name: 'mockito-all', version: '1.8.5'
        testCompile group: 'org.easytesting', name: 'fest-assert', version: '1.4'
    }
}

project(':dorm') {
    apply plugin: 'war'

    dependencies {
        compile project(':dorm-core'), project(':dorm-maven')
    }
}

project(':dorm-maven') {
    dependencies {
        compile project(':dorm-core')
        testCompile project(path: ':dorm-core', configuration: 'sharedTests')
    }
}

project(':dorm-core-test') {
    dependencies {
        compile project(':dorm-core')
    }
}